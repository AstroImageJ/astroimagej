plugins {
    id 'java-library'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

dependencies {
    implementation project(':Nom_Fits')
    implementation 'org.openjdk.nashorn:nashorn-core:15.4'
    api 'de.rototor.pdfbox:graphics2d:0.38'
    api 'net.java.dev.jna:jna-platform:5.12.1'
}

def mainClassName = 'ij.ImageJ'

processResources {
    inputs.property "version", project.version

    filesMatching("aij_version.txt") {
        expand "version": project.version
    }
}

jar {
    FileTree m = fileTree("${rootProject.projectDir}/src/main/resources/plugins/")
    Collection n = m.include("**/*.jar").grep()

    archiveFileName = 'ij-standalone.jar'

    manifest {
        attributes("Main-Class": mainClassName,
                   "Class-Path": "plugins/ ${n.collect {'plugins/' + ((File)it).getName()}.join(' ')}")
    }
}

shadowJar {
    outputs.upToDateWhen {false}
    archiveFileName = 'ij.jar'

    // Don't ship non-windows binaries
    exclude "**/*.jnilib"
    exclude "**/*.so"
    exclude "**/*.a"
}